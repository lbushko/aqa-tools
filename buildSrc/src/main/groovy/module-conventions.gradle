plugins {
    id "java"
    id "maven-publish"
    id "java-library"
    id "org.jreleaser"
}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            artifactId = project.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = "AQA Tools"
                description = "Advanced AQA Tools, Utils, Annotations"
                url = 'https://github.com/lbushko/aqa-tools'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = 'lbushko'
                        name = 'Lyubomyr Bushko'
                        email = 'lybomur.bushko@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/lbushko/aqa-tools.git'
                    developerConnection = 'scm:git@github.com:lbushko/aqa-tools.git'
                    url = 'https://github.com/lbushko/aqa-tools'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
//            nexus2 {
//                'maven-central' {
//                    active = 'ALWAYS'
//                    url = 'https://s01.oss.sonatype.org/service/local'
//                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
//                    closeRepository = true
//                    releaseRepository = true
//                    stagingRepository('build/staging-deploy')
//                }
//            }
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}